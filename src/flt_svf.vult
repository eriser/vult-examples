

fun calc_g(cv) @[table(size=32, min=-0.3, max=0.7)]{
   val pitch = cv * 120.0 + 48.0;
   val f = 8.175798915643707*exp(0.057762265046662105*pitch);
   val pi = 3.141592653589793;
   val wd = 2.0*pi*f;
	val T  = 1.0/44100.0;
	val wa = (2.0/T)*tan(wd*T/2.0);
	val g  = wa*T/2.0;
   return g;
}

fun flt_svf(x,cv,q,sel:int) {
   mem z1, z2;
   mem g,inv_den, R;
   q = q + 0.5;
   if(Util.change(cv) || Util.change(q)) {
      g = calc_g(cv);
      R = 1.0/(2.0*(q+eps()));
      inv_den = 1.0/(1.0 + 2.0*R*g + g*g);
   }

   val high  = (x - (2.0*R + g)*z1 - z2) * inv_den;
	val band  = g*high + z1;
	val low   = g*band + z2;
   val notch = low + high;

	z1 = g*high + band;
	z2 = g*band + low;

   val output =
      if sel == 0 then low else
      if sel == 1 then high else
      if sel == 2 then band else
      notch;
   return Util.soft_saturate(output);
}
and init() @[init] {
   g = calc_g(0.0);
   R = 1.0/(2.0*0.5);
   inv_den = 1.0/(1.0 + 2.0*0.0*g + g*g);
}

fun process(input,cv,q,sel) {
   return flt_svf(input,cv,q,sel);
}
and noteOn(note:int,velocity:int,channel:int){ }
and noteOff(note:int,channel:int){ }
and controlChange(control:int,value:int,channel:int){ }
and default(){ }