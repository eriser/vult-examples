
fun edge(x) {
   mem pre;
   val ret = x && not(pre);
   pre = x;
   return ret;
}

fun change(x:real):bool {
    mem pre_x;
    val v:bool = pre_x <> x;
    pre_x = x;
    return v;
}

fun map(x:real,x0,x1,y0,y1) {
  return (x-x0)*(y1-y0)/(x1-x0) + y0;
}

// Returns true if the input value is near zero (< 1e-2)
fun near_zero(x) : bool return abs(x)<1e-9;

fun dcblock(x0){
  mem x1,y1;
  val y0 = x0-x1+y1*0.995;
  x1,y1 = x0,y0;
  return y0;
}

fun smooth(input){
   mem x;
   x = x+(input-x)*0.005;
   return x;
}

fun average2(x1:real) : real {
   mem x0;
   val result = (x0 + x1) / 2.0;
   x0 = x1;
   return result;
}

fun sine_table(x) @[table(size=128,min=0.0,max=1.0)] {
   return sin(6.283185307179586 * x);
}


fun saturate_table(x) @[table(size=10,min=0.0,max=6.0)] {
  return tanh(x);
}

fun saturate(x) {
  if(x==0.0) return 0.0;
  val fixed, sign = if x > 0.0 then x,false else -x, true;
  val out = 0.0;
  if(fixed > 6.0) {
    out = 1.0;
  }
  else {
    out = saturate_table(fixed);
  }
  out = if sign then -out else out;
  return out;
}

fun soft_saturate_table(x) @[table(size=10,min=0.0,max=6.0)] {
  return 16.0 * tanh(x / 16.0);
}

fun soft_saturate(x) {
  if(x==0.0) return 0.0;
  val fixed, sign = if x > 0.0 then x,false else -x, true;
  val out = 0.0;
  if(fixed > 6.0) {
    out = 1.0;
  }
  else {
    out = soft_saturate_table(fixed);
  }
  out = if sign then -out else out;
  return out;
}

/* Rate to get a 2^14 ramp */
fun pitchToRate(pitch) @[table(size=32,min=0.0,max=127.0)] {
   // 2^10 /44100 440 2^((pitch - 69)/12.)
   return 0.18984168003671556 * exp(0.057762265046662105 * pitch);
}

fun cvToPitch(cv) {
   return cv * 120.0 + 24.0;
}

fun cvToRate(cv) @[table(size=32,min=0.0,max=0.9)] {
   return pitchToRate(cvToPitch(cv));
}

fun pitchToCv(pitch) {
   return 1.0/120.0 * (-24.0 + pitch);
}

fun cvToRate_unit(cv) @[table(size=32,min=0.0,max=0.9)] {
   return pitchToRate(cvToPitch(cv))/1024.0;
}