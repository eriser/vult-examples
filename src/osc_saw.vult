

fun pitchToRate(d) return 8.1758*exp(0.0577623*d)/44100.0;

fun cvToRate(cv) {
   return pitchToRate(cv * 12.0 + 48.0);
}

fun change(x):bool {
    mem pre_x;
    val v:bool = pre_x <> x;
    pre_x = x;
    return v;
}

fun osc_saw(cv) {
   mem inc;
   if(change(cv))
      inc = cvToRate(cv);
   val i = if inc < eps() then eps() else inc;
   // generater a ramp from -1.0  to 1.0
   mem phase = (2.0*inc + phase) % 2.0;
   val ph = phase - 1.0;
   val o = 0.0;
   // soften the transitions
   if(ph >= 1.0 - i) {
      val b = (1.0 - i - ph)/i;
      o = b * abs(b) + ph;
   }
   else if(ph <= i - 1.0) {
      val b = (i - 1.0 - ph)/i;
      o = b * abs(b) + ph;
   }
   else {
      o = ph;
   }
   return o;
}
and init()@[init]{
   inc = cvToRate(0.0);
}

fun process(cv) {
   return osc_saw(cv);
}
and noteOn(note:int,velocity:int,channel:int){ }
and noteOff(note:int,channel:int){ }
and controlChange(control:int,value:int,channel:int){ }
and default(){ }